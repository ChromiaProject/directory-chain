struct provider_node_data {
    pubkey;
    active: boolean;
    host: text;
    port: integer;
    api_url: text;
    last_updated: timestamp;
    territory: text?;
}

query get_nodes_by_provider(pubkey) {
    return node @* { provider @ { pubkey } } (
        provider_node_data(
            .pubkey,
            .active,
            .host,
            .port,
            .api_url,
            .last_updated,
            .territory
        )
    );
}

query get_provider_points(pubkey): integer {
    return provider_rl_state @ {provider @ { pubkey }} .points;
}

query get_provider_clusters(pubkey) : list<text> {
    return cluster_provider @* { provider @ {pubkey} }.cluster.name;
}

query get_provider_data (pubkey) {
    return provider @ { pubkey } ($.to_struct());
}

query get_all_providers(): list<struct<provider>> {
    return provider @* {} ($.to_struct());
}

query get_providers(tier: provider_tier, system: boolean, require_active: boolean): list<struct<provider>> {
    if (system) {
        require(tier == provider_tier.NODE_PROVIDER, "System providers are always node providers");
    }
    return  provider @* { 
        tier,
        if (system) .system else true, 
        if (require_active) .active else true 
     } ($.to_struct());
}

query get_provider_quotas(): list<struct<provider_quota>> {
    return provider_quota @* {} ($.to_struct());
}
