@extend(receive_icmf_message) function receive_configuration_updated(sender: byte_array, topic: text, body: gtv) {
    if (topic != configuration_updated_topic) return;

    val message = configuration_updated.from_gtv(body);

    if (message.blockchain_rid == chain_context.blockchain_rid) {
        log("Received updated configuration from chain %s for chain0 at height %d to %s, ignoring"
            .format(sender, message.height, message.config_hash));
        return;
    }
    val bc = blockchain @? { message.blockchain_rid };
    if (bc == null) {
        log("Unknown blockchain " + message.blockchain_rid);
        return;
    }

    val cluster = get_cluster_for_blockchain(message.blockchain_rid);
    val anchoring_chain = cluster_anchoring_chain @? { cluster } (.blockchain);
    if (anchoring_chain == null) {
        log("No anchoring chain for cluster %s".format(cluster.name));
        return;
    }
    if (sender != anchoring_chain.rid and sender != system_anchoring_chain.rid) {
        log("Received updated configuration from chain %s, which is not anchor chain for cluster %s".format(sender, cluster.name));
        return;
    }

    log("Received updated configuration from chain %s for chain %s at height %d to %s"
        .format(sender, message.blockchain_rid, message.height, message.config_hash));

    val pending_configuration = pending_blockchain_configuration @?
        { bc, .config_hash == message.config_hash, .minimum_height <= message.height };
    if (pending_configuration == null) {
        log("Configuration with hash %s and minimum_height<=%d not found for chain %s".format(message.config_hash, message.height, message.blockchain_rid));
        return;
    }

    val pending_base_config = pending_configuration.base_config;
    val pending_signers = pending_configuration.signers;
    if (blockchain_configuration @? { bc, message.height } != null) {
        log("Configuration at height %d already exists for chain %s".format(message.height, message.blockchain_rid));
        return;
    }

    delete signer_excluded_from_pending_configuration @* { bc, .config_hash == pending_configuration.config_hash };
    delete pending_configuration;

    create blockchain_configuration(bc, message.height, pending_base_config);

    val bc_signers = blockchain_configuration_signers @? { bc, message.height };
    if (bc_signers == null) {
        create blockchain_configuration_signers(bc, message.height, pending_signers);
    } else {
        bc_signers.signers = pending_signers;
    }

    add_dependencies(pending_base_config, bc.rid, message.height);
}

operation __begin_block(height: integer) {
    if (empty(blockchain @? { chain_context.blockchain_rid })) return;
    val expected_config = get_blockchain_configuration(chain_context.blockchain_rid, height);
    if (expected_config == null or expected_config.config_hash == chain_context.raw_config.hash()) return;

    log("Chain0 config for height %d changed from %s to %s".format(
        height, expected_config.config_hash, chain_context.raw_config.hash()));
    val config_map = map<text, gtv>.from_gtv(chain_context.raw_config);
    config_map.remove_or_null("signers");
    val base_config = config_map.to_gtv().to_bytes();
    create blockchain_configuration(blockchain @ { chain_context.blockchain_rid }, height, base_config);
    add_dependencies(base_config, chain_context.blockchain_rid, height);

    after_begin_block(height);
}

@extendable function after_begin_block(height: integer) {}
