@test module;

import ^.config_test_helpers.*;
import ^^.*;
import ^^.operations.*;
import ^^.test.ops.*;
import ^^.test.setup.*;
import ^^.test.util.*;
import c: ^^.test.util.constants.*;
import nm_api.*;
import proposal_blockchain.*;
import proposal_provider.*;
import direct_container.*;

function test_precise_configuration_update_config_happy() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_equals(
        pending_config1.signers,
        [c.test_node.pubkey]
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 1, pending_config1_hash));
    val update_attempt = blockchain_configuration_update_attempt @ { .config_hash == pending_config1_hash };
    assert_equals(update_attempt.state, blockchain_configuration_update_state.PENDING);

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();
    val updated_config = require(nm_get_blockchain_configuration_v5(bc_rid, 5));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(updated_config.base_config))["number"],
        (1).to_gtv()
    );
    assert_equals(
        updated_config.signers,
        [c.test_node.pubkey]
    );
    assert_true(nm_get_pending_blockchain_configuration(bc_rid, 5).empty());
    assert_true(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
    assert_equals(update_attempt.state, blockchain_configuration_update_state.SUCCESSFUL);
    // Assert no redundant signer config entity is created
    assert_null(blockchain_configuration_signers @? { .height == 5 });
}

function test_precise_configuration_update_config_happy_unique_config() {
    val (anchor_chain, bc_rid) = setup();

    val config = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];

    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config.to_gtv_pretty().to_bytes(), "first")
    ).sign(initial_provider).run();

    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config.to_gtv_pretty().to_bytes(), "second")
    ).sign(initial_provider).run();

    val pending_configs = nm_get_pending_blockchain_configuration(bc_rid, 5);
    assert_equals(pending_configs.size(), 2);
    assert_not_equals(pending_configs[0].base_config, pending_configs[1].base_config);
}

function test_precise_configuration_update_signers_happy() {
    val (anchor_chain, bc_rid) = setup();

    rell.test.tx().op(
        register_provider(initial_provider.pub, rell.test.pubkeys.bob, provider_tier.NODE_PROVIDER),
        propose_provider_state(initial_provider.pub, rell.test.pubkeys.bob, true, ""),
        propose_provider_is_system(initial_provider.pub, rell.test.pubkeys.bob, true, "")
    ).sign(initial_provider).run();
    val second_provider = require_provider(rell.test.pubkeys.bob);
    val system_cluster = require_cluster(clusters.system);
    val second_node_pubkey = rell.test.pubkeys.trudy;
    rell.test.tx().op(
        create_test_node(second_provider, second_node_pubkey, system_cluster)
    ).sign(rell.test.keypairs.bob).run();
    rell.test.tx().op(
        add_node_to_cluster(rell.test.pubkeys.bob, second_node_pubkey, clusters.system)
    ).sign(rell.test.keypairs.bob).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();
    assert_equals(pending_config1.minimum_height, 1);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        pending_config1.signers,
        [c.test_node.pubkey, second_node_pubkey].sorted()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 1, pending_config1_hash));

    // Assert new pending configs are "invisible"
    val new_config = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, new_config.to_gtv_pretty().to_bytes(), "first")
    ).sign(initial_provider).run();
    val pending_configs = nm_get_pending_blockchain_configuration(bc_rid, 5);
    assert_equals(nm_get_pending_blockchain_configuration(bc_rid, 2).size(), 1);
    assert_equals(pending_configs[0], pending_config1);

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();
    val updated_config = require(nm_get_blockchain_configuration_v5(bc_rid, 5));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(updated_config.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        updated_config.signers,
        [c.test_node.pubkey, second_node_pubkey].sorted()
    );
    assert_true(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
    // Assert signer config entity is created
    assert_equals(blockchain_configuration_signers @ { .height == 5 }.signers, full_config1["signers"].to_bytes());
    // Assert new pending configs are "visible" again
    val pending_configs_after = nm_get_pending_blockchain_configuration(bc_rid, 5);
    assert_equals(pending_configs_after.size(), 1);
    assert_not_equals(pending_configs_after[0], pending_config1);
}

function test_precise_configuration_update_signers_retry() {
    val (anchor_chain, bc_rid) = setup();

    rell.test.tx().op(
        register_provider(initial_provider.pub, rell.test.pubkeys.bob, provider_tier.NODE_PROVIDER),
        propose_provider_state(initial_provider.pub, rell.test.pubkeys.bob, true, ""),
        propose_provider_is_system(initial_provider.pub, rell.test.pubkeys.bob, true, "")
    ).sign(initial_provider).run();
    val second_provider = require_provider(rell.test.pubkeys.bob);
    val system_cluster = require_cluster(clusters.system);
    val second_node_pubkey = rell.test.pubkeys.trudy;
    rell.test.tx().op(
        create_test_node(second_provider, second_node_pubkey, system_cluster)
    ).sign(rell.test.keypairs.bob).run();
    rell.test.tx().op(
        add_node_to_cluster(rell.test.pubkeys.bob, second_node_pubkey, clusters.system)
    ).sign(rell.test.keypairs.bob).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();
    assert_equals(pending_config1.minimum_height, 1);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        pending_config1.signers,
        [c.test_node.pubkey, second_node_pubkey].sorted()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 1, pending_config1_hash));

    rell.test.tx().op(
        receive_configuration_failed_op(
            anchor_chain.rid,
            configuration_failed_topic,
            configuration_failed(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();
    // Assert that a retry has been created
    assert_equals(nm_get_pending_blockchain_configuration(bc_rid, 5).size(), 1);
    val pending_config1_retry = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1_retry = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1_retry.base_config));
    full_config1_retry["signers"] = pending_config1_retry.signers.to_gtv();
    val pending_config1_hash_retry = full_config1_retry.to_gtv().hash();
    assert_equals(pending_config1_retry.minimum_height, 1);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1_retry.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        pending_config1_retry.signers,
        [c.test_node.pubkey, second_node_pubkey].sorted()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 1, pending_config1_hash_retry));

    // Complete retry
    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=8,
                config_hash=pending_config1_hash_retry
            ).to_gtv()
        )
    ).run();

    val updated_config = require(nm_get_blockchain_configuration_v5(bc_rid, 8));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(updated_config.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        updated_config.signers,
        [c.test_node.pubkey, second_node_pubkey].sorted()
    );
    assert_true(is_pending_blockchain_configuration_applied(bc_rid, 8, pending_config1_hash));
    // Assert signer config entity is created
    assert_equals(blockchain_configuration_signers @ { .height == 8 }.signers, full_config1["signers"].to_bytes());
}

function test_precise_configuration_update_config_multiple_pending() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );

    val config2 = ["number": (2).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config2.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_configs = nm_get_pending_blockchain_configuration(bc_rid, 5);
    assert_equals(pending_configs.size(), 2);
    assert_equals(pending_configs[0].minimum_height, 1);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_configs[0].base_config))["number"],
        (1).to_gtv()
    );
    assert_equals(pending_configs[1].minimum_height, 2);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_configs[1].base_config))["number"],
        (2).to_gtv()
    );
    val full_config2 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_configs[1].base_config));
    full_config2["signers"] = pending_configs[1].signers.to_gtv();
    val pending_config2_hash = full_config2.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();
    val updated_config1 = require(nm_get_blockchain_configuration_v5(bc_rid, 5));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(updated_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_equals(
        updated_config1.signers,
        [c.test_node.pubkey]
    );
    assert_equals(nm_get_pending_blockchain_configuration(bc_rid, 5).size(), 1);
    assert_true(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=7,
                config_hash=pending_config2_hash
            ).to_gtv()
        )
    ).run();
    val updated_config2 = require(nm_get_blockchain_configuration_v5(bc_rid, 7));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(updated_config2.base_config))["number"],
        (2).to_gtv()
    );
    assert_equals(
        updated_config2.signers,
        [c.test_node.pubkey]
    );
    assert_equals(nm_get_pending_blockchain_configuration(bc_rid, 7).size(), 0);
    assert_true(is_pending_blockchain_configuration_applied(bc_rid, 7, pending_config2_hash));
}

function test_precise_configuration_update_rejected_invalid_config_no_config_consensus_strategy() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv()];
    val failure = rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run_must_fail();
    assert_true(failure.message.contains("Configuration must contain \"config_consensus_strategy\"=\"HEADER_HASH\""));
}

function test_precise_configuration_update_rejected_invalid_config_wrong_config_consensus_strategy() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "bogus".to_gtv()];
    val failure = rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run_must_fail();
    assert_true(failure.message.contains("Configuration must contain \"config_consensus_strategy\"=\"HEADER_HASH\""));
}

function test_precise_configuration_update_rejected_invalid_config_has_signers() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv(),
        "signers": [x"1111".to_gtv(), x"2222".to_gtv()].to_gtv()];
    val failure = rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run_must_fail();
    assert_true(failure.message.contains("Configuration must not contain \"signers\""));
}

function test_precise_configuration_update_ignored_wrong_topic() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            "bogus_topic",
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_ignored_chain0() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=chain_context.blockchain_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_ignored_unknown_blockchain() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=pending_config1_hash,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_rejected_not_from_anchor_chain() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            bc_rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_rejected_wrong_hash() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash="bogus".hash()
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_rejected_config_already_exists() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 1) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();

    val config2 = ["number": (2).to_gtv()];
    rell.test.tx().op(
        propose_configuration_at(initial_provider.pub, bc_rid, config2.to_gtv_pretty().to_bytes(), 5, false)
    ).sign(initial_provider).run();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (2).to_gtv()
    );

    rell.test.tx().op(
        receive_configuration_updated_op(
            anchor_chain.rid,
            configuration_updated_topic,
            configuration_updated(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();

    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (2).to_gtv()
    );
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 5, pending_config1_hash));
}

function test_precise_configuration_update_config_failed() {
    val (anchor_chain, bc_rid) = setup();

    val config1 = ["number": (1).to_gtv(), "txqueuecapacity": "bogus".to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_configuration(initial_provider.pub, bc_rid, config1.to_gtv_pretty().to_bytes(), "")
    ).sign(initial_provider).run();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 5)).base_config))["number"],
        (0).to_gtv()
    );
    val pending_config1 = nm_get_pending_blockchain_configuration(bc_rid, 5) @ {};
    val full_config1 = map<text, gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config));
    full_config1["signers"] = pending_config1.signers.to_gtv();
    val pending_config1_hash = full_config1.to_gtv().hash();
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(pending_config1.base_config))["number"],
        (1).to_gtv()
    );
    assert_equals(
        pending_config1.signers,
        [c.test_node.pubkey]
    );
    assert_false(is_pending_blockchain_configuration_applied(bc_rid, 1, pending_config1_hash));
    val update_attempt = blockchain_configuration_update_attempt @ { .config_hash == pending_config1_hash };
    assert_equals(update_attempt.state, blockchain_configuration_update_state.PENDING);

    rell.test.tx().op(
        receive_configuration_failed_op(
            anchor_chain.rid,
            configuration_failed_topic,
            configuration_failed(
                blockchain_rid=bc_rid,
                height=5,
                config_hash=pending_config1_hash
            ).to_gtv()
        )
    ).run();
    val not_updated_config = require(nm_get_blockchain_configuration_v5(bc_rid, 5));
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(not_updated_config.base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        not_updated_config.signers,
        [c.test_node.pubkey]
    );
    assert_equals(nm_get_faulty_blockchain_configuration(bc_rid, 5), pending_config1_hash);
    assert_true(nm_get_pending_blockchain_configuration(bc_rid, 5).empty());
    assert_equals(update_attempt.state, blockchain_configuration_update_state.FAILED);
}

function setup(): (anchor_chain: blockchain, bc_rid: byte_array) {
    setup_module(cluster_anchoring_config = ["any": "value".to_gtv()]);

    val cluster = require_cluster(clusters.system);
    val anchor_chain = cluster_anchoring_chain @ { cluster } (.blockchain);

    val config0 = ["number": (0).to_gtv(), "config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        create_container(initial_provider.pub, "container", clusters.system, 0, [initial_provider.pub]),
        propose_blockchain(initial_provider.pub, config0.to_gtv_pretty().to_bytes(), "foo_chain", "container", "")
    ).sign(initial_provider).run();
    val bc_rid = blockchain @ { "foo_chain" } (.rid);
    assert_equals(
        map<text,gtv>.from_gtv(gtv.from_bytes(require(nm_get_blockchain_configuration_v5(bc_rid, 1)).base_config))["number"],
        (0).to_gtv()
    );
    assert_equals(
        (require(nm_get_blockchain_configuration_v5(bc_rid, 1))).signers,
        [c.test_node.pubkey]
    );
    assert_true(nm_get_pending_blockchain_configuration(bc_rid, 1).empty());

    return (anchor_chain=anchor_chain, bc_rid=bc_rid);
}
