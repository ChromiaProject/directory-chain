@test module;

import ^.setup.*;
import ^.util.*;
import ^^.*;
import ^^.operations.*;
import ^^.queries.*;
import proposal_provider.*;

val system_provider = rell.test.keypairs.bob;
val node_provider1 = rell.test.keypairs.trudy;
val node_provider2 = rell.test.keypair(
     priv = x"B39E4CCCB9B0FD71B7AC9FFF1216C34648FA7362ECD9874743776BDFDEE4F6A8",
     pub = x"03BD01AF899839BDDB5810616C1AF8B61B415E903D73418DED238A8EEA352C72E0"
);
val dapp_provider1 = rell.test.keypair(
    priv = x"71C89AE77719E3779EAE09C26A229F7AF33D8C8F7E7E8D82A9A42470C224AB56",
    pub = x"020EE9063CED9B47CC60EB931443C01F3FD7A0DAB4B2D0505D3A2FAAD4FA9ABF13"
);
val dapp_provider2 = rell.test.keypair(
    priv = x"49DB53CB0E1CC472DE90FC833CEDB4E1B412C5AF658342762D1BB8F3886063B6",
    pub = x"031D22EA1C599A69E35E023E1CBCEAC66C046B445FF5507DBA650B58F28C650A15"
);

function test_register_provider() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider1.pub, provider_tier.DAPP_PROVIDER)
    ).sign(initial_provider).run();

    val np1 = provider @ { node_provider1.pub };
    assert_false(np1.active);
    assert_false(roles.has_node_access(np1));

    val dp1 = provider @ { dapp_provider1.pub };
    assert_true(dp1.active); // DP is active by default if creator is NP
    assert_false(roles.has_node_access(dp1));

    // DP registers DP
    rell.test.tx().op(
        register_provider(dapp_provider1.pub, dapp_provider2.pub, provider_tier.DAPP_PROVIDER)
    ).sign(dapp_provider1).run();
    val dp2 = provider @ { dapp_provider2.pub };
    assert_true(dp2.active); // DP is always active by default
    assert_false(roles.has_node_access(dp2));

    // DP tries to register NP/SP and fails, since only SP can register NP
    rell.test.tx().op(
        register_provider(dapp_provider1.pub, node_provider2.pub, provider_tier.NODE_PROVIDER)
    ).sign(dapp_provider1).run_must_fail();

    // NP registers DP
    val pk3 = x"aaaaaaaaaaaaaaaaaaaaaaaaaaaE4ABFB92936540FDF9B9F1BC7CEB51481286E31";
    rell.test.tx().op(
        register_provider(node_provider1.pub, pk3, provider_tier.DAPP_PROVIDER)
    ).sign(node_provider1).run();
    val dp3 = provider @ { pk3 };
    assert_true(dp3.active); // DP is active by default
    assert_false(roles.has_node_access(dp3));

    // NP tries to register NP and fails, since only SP can register NP
    rell.test.tx().op(
        register_provider(node_provider1.pub, node_provider2.pub, provider_tier.NODE_PROVIDER)
    ).sign(node_provider1).run_must_fail();

    // SP registers DP, NP
    val pk4 = x"bbbbbbbbbbbbbbbbbbbbbbbbbbbbbb8C20DAE90B217387662503B4C54D50D438E1";
    rell.test.tx().op(
        register_provider(initial_provider.pub, pk4, provider_tier.DAPP_PROVIDER),
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER)
    ).sign(initial_provider).run();

    val dp4 = provider @ { pk4 };
    assert_true(dp4.active); // DP is active by default
    assert_false(roles.has_node_access(dp4));

    val np2 = provider @ { node_provider2.pub };
    assert_false(np2.active); // NP is not active by default
    assert_false(roles.has_node_access(np2));

    // Already exists
    rell.test.tx().op(
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER)
    ).sign(initial_provider).run_must_fail();

    assert_equals(get_providers(provider_tier.DAPP_PROVIDER, false, true).size(), 4);
    assert_equals(get_providers(provider_tier.NODE_PROVIDER, false, true).size(), 1);
    assert_equals(get_providers(provider_tier.NODE_PROVIDER, true, true).size(), 1);
    assert_equals(get_providers(provider_tier.NODE_PROVIDER, false, false).size(), 3);
    assert_fails(get_providers(provider_tier.DAPP_PROVIDER, true, true, *));
}

function test_enable_provider() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, system_provider.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider1.pub, provider_tier.DAPP_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider2.pub, provider_tier.DAPP_PROVIDER)
    ).sign(initial_provider).run();

    // DP/Dapp providers are active by default
    val dp1 = provider @ { dapp_provider1.pub };
    assert_true(dp1.active);
    assert_false(roles.has_node_access(dp1));

    val dp2 = provider @ { dapp_provider2.pub };
    assert_true(dp2.active);
    assert_false(roles.has_node_access(dp2));

    // NP must be activated first
    val np1 = provider @ { node_provider1.pub };
    assert_false(np1.active);
    assert_false(roles.has_node_access(np1));

    val np2 = provider @ { node_provider2.pub };
    assert_false(np2.active);
    assert_false(roles.has_node_access(np2));

    // SP promotes SP
    rell.test.tx().op(
        propose_provider_is_system(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();
    val sp = provider @ { system_provider.pub };
    assert_true(sp.active);
    assert_true(roles.has_system_access(sp));

    // DP tries to activate DP, NP, SP and fails
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP | (-) | (-) | (-)
      NP  |  *  |  *  |  *
      SP  |  *  |  *  |  *
    */
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, dapp_provider2.pub, true)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, node_provider2.pub, true)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, initial_provider.pub, true)
    ).sign(dapp_provider1).run_must_fail();

    // NP tries to activate DP and fails because it is not activated
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  | (*) |  *  |  *
      SP  |  *  |  *  |  *
    */
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, dapp_provider1.pub, true)
    ).sign(node_provider1).run_must_fail();

    // SP activates NP
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  | -
      NP  |  *  |  *  |  *
      SP  |  *  | (+) |  *
    */
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, node_provider1.pub, true)
    ).sign(initial_provider).run();
    rell.test.tx().op(
        make_vote(system_provider.pub, last_proposal().rowid.to_integer(), true)
    ).sign(system_provider).run();
    assert_true(np1.active);
    assert_true(roles.has_node_access(np1));

    // SP activates DP (it's activated by default, just checking requirements)
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  |  *  |  *  |  *
      SP  | (+) |  +  |  *
    */
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, dapp_provider1.pub, true)
    ).sign(initial_provider).run();

    // NP activates DP (it's activated by default, just checking requirements), NP, SP
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  | (+) | (-) | (-)
      SP  |  +  |  +  |  *
    */
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, dapp_provider1.pub, true)
    ).sign(node_provider1).run();
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, node_provider2.pub, true)
    ).sign(node_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, initial_provider.pub, true)
    ).sign(initial_provider).run_must_fail();

    // SP activates SP
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  |  +  |  -  |  -
      SP  |  +  |  +  | (+)
    */
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();
    assert_true(sp.active);
    assert_true(roles.has_system_access(sp));

    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  |  +  |  -  |  -
      SP  |  +  |  +  |  +
    */
}

function test_disable_provider() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, system_provider.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider1.pub, provider_tier.DAPP_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider2.pub, provider_tier.DAPP_PROVIDER)
    ).sign(initial_provider).run();

    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, system_provider.pub, true),
        propose_provider_is_system(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();

    val sp = provider @ { system_provider.pub };
    assert_true(sp.active);

    val np1 = provider @ { node_provider1.pub };
    _enable_provider(np1, system_provider);
    assert_true(np1.active);

    val np2 = provider @ { node_provider2.pub };
    _enable_provider(np2, system_provider);
    assert_true(np2.active);

    val dp1 = provider @ { dapp_provider1.pub };
    assert_true(dp1.active);

    val dp2 = provider @ { dapp_provider2.pub };
    assert_true(dp2.active);


    // DP tries to deactivate DP, NP, SP and fails
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP | (-) | (-) | (-)
      NP  |  x  |  x  |  x
      SP  |  x  |  x  |  x
    */
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, dapp_provider2.pub, false)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, node_provider2.pub, false)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(dapp_provider1.pub, initial_provider.pub, false)
    ).sign(dapp_provider1).run_must_fail();

    // NP tries to deactivate DP, NP, SP
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  | (+) | (-) | (-)
      SP  |  x  |  x  |  x
    */
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, dapp_provider2.pub, false)
    ).sign(node_provider1).run();
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, node_provider2.pub, false)
    ).sign(node_provider1).run_must_fail();
    rell.test.tx().op(
        propose_provider_state(node_provider1.pub, initial_provider.pub, false)
    ).sign(node_provider1).run_must_fail();

    // SP tries to deactivate DP, NP, SP
    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  |  +  |  -  |  -
      SP  | (+) | (+) | (+)
    */
    // Making sure providers are enabled
    _enable_provider(dp1);
    assert_true(dp1.active);
    assert_true(np1.active);
    assert_true(sp.active);
    // Disabling providers
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, dapp_provider1.pub, false)
    ).sign(initial_provider).run();
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, node_provider1.pub, false)
    ).sign(initial_provider).run();
    _provider_votes(system_provider);
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, system_provider.pub, false)
    ).sign(initial_provider).run();
    _provider_votes(system_provider);
    // Asserting
    assert_false(dp1.active);
    assert_false(np1.active);
    assert_false(sp.active);

    /*
          | DP | NP  | SP
     -----|-----|-----|-----
      DP |  -  |  -  |  -
      NP  |  +  |  -  |  -
      SP  |  +  |  +  |  +
    */
}

function test_system_provider_fails_to_disable_other_system_provider() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, system_provider.pub, provider_tier.NODE_PROVIDER),
        propose_provider_is_system(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();

    val sp = provider @ { system_provider.pub };
    assert_true(sp.active);
    assert_true(sp.system);

    // New provider tries to demote initial provider
    rell.test.tx().op(
        propose_provider_is_system(system_provider.pub, initial_provider.pub, false)
    ).sign(system_provider).run();

    // Initial provider rejects it
    rell.test.tx().op(
            make_vote(initial_provider.pub, last_proposal().rowid.to_integer(), false)
    ).sign(initial_provider).run();

    // Ensure initial provider is still a system provider
    val ip = provider @ { initial_provider.pub };
    assert_true(ip.active);
    assert_true(ip.system);
}

function test_promote_node_provider() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, system_provider.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider1.pub, provider_tier.DAPP_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider2.pub, provider_tier.DAPP_PROVIDER),
        propose_provider_state(initial_provider.pub, system_provider.pub, true),
        propose_provider_is_system(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();

    // DP tries to promote DP, NP, SP and fails
    rell.test.tx().op(
        promote_node_provider(dapp_provider1.pub, dapp_provider2.pub)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        promote_node_provider(dapp_provider1.pub, node_provider1.pub)
    ).sign(dapp_provider1).run_must_fail();
    rell.test.tx().op(
        promote_node_provider(dapp_provider1.pub, system_provider.pub)
    ).sign(dapp_provider1).run_must_fail();

    // NP tries to promote DP, NP, SP and fails
    rell.test.tx().op(
        promote_node_provider(node_provider1.pub, dapp_provider2.pub)
    ).sign(node_provider1).run_must_fail();
    rell.test.tx().op(
        promote_node_provider(node_provider1.pub, node_provider2.pub)
    ).sign(node_provider1).run_must_fail();
    rell.test.tx().op(
        promote_node_provider(node_provider1.pub, system_provider.pub)
    ).sign(node_provider1).run_must_fail();

    // SP promotes DP, NP, SP
    // * SP > DP
    rell.test.tx().op(
        promote_node_provider(system_provider.pub, dapp_provider2.pub)
    ).sign(system_provider).run();
    val promoted_provider = provider @ { dapp_provider2.pub };
    assert_equals(promoted_provider.tier, provider_tier.NODE_PROVIDER);
    // Assert that promoted provider needs voting to be enabled
    assert_equals(promoted_provider.active, false);

    // * SP > NP, fails since NP is already a node provider
    rell.test.tx().op(
        promote_node_provider(system_provider.pub, node_provider2.pub)
    ).sign(system_provider).run_must_fail();
    // * IP(SP) > SP, fails since SP is already a node provider
    rell.test.tx().op(
        promote_node_provider(initial_provider.pub, system_provider.pub)
    ).sign(initial_provider).run_must_fail();

    // SP promotes itself and fails
    rell.test.tx().op(
        promote_node_provider(system_provider.pub, system_provider.pub)
    ).sign(system_provider).run_must_fail();
}

function test_transfer_action_points() {
    setup_module();

    rell.test.tx().op(
        register_provider(initial_provider.pub, system_provider.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, node_provider2.pub, provider_tier.NODE_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider1.pub, provider_tier.DAPP_PROVIDER),
        register_provider(initial_provider.pub, dapp_provider2.pub, provider_tier.DAPP_PROVIDER),
        propose_provider_state(initial_provider.pub, system_provider.pub, true),
        propose_provider_is_system(initial_provider.pub, system_provider.pub, true)
    ).sign(initial_provider).run();

    // Asserting initial state
    val sp = provider @? { system_provider.pub };
    val np1 = provider @? { node_provider1.pub };
    val np2 = provider @? { node_provider2.pub };
    val dp1 = provider @? { dapp_provider1.pub };
    val dp2 = provider @? { dapp_provider2.pub };

    assert_equals(_points(sp), 100);
    assert_equals(_points(np1), 100);
    assert_equals(_points(np2), 100);
    assert_equals(_points(dp1), 100);
    assert_equals(_points(dp2), 100);

    // Bad op sign
    rell.test.tx().op(
        transfer_action_points(dapp_provider1.pub, dapp_provider2.pub, 10)
    ).sign(initial_provider).run_must_fail();

    // DP -> itself, DP, NP, SP
    rell.test.tx().op(
        transfer_action_points(dapp_provider1.pub, dapp_provider1.pub, 10),
        transfer_action_points(dapp_provider1.pub, dapp_provider2.pub, 10),
        transfer_action_points(dapp_provider1.pub, node_provider1.pub, 10),
        transfer_action_points(dapp_provider1.pub, system_provider.pub, 10)
    ).sign(dapp_provider1).run();
    assert_equals(_points(dp1), 66);
    assert_equals(_points(dp2), 110);
    assert_equals(_points(np1), 110);
    assert_equals(_points(sp), 110);

    // NP -> DP, itself, NP, SP
    rell.test.tx().op(
        transfer_action_points(node_provider2.pub, dapp_provider2.pub, 10),
        transfer_action_points(node_provider2.pub, node_provider2.pub, 10),
        transfer_action_points(node_provider2.pub, node_provider1.pub, 10),
        transfer_action_points(node_provider2.pub, system_provider.pub, 10)
    ).sign(node_provider2).run();
    assert_equals(_points(np2), 66);
    assert_equals(_points(dp2), 120);
    assert_equals(_points(np1), 120);
    assert_equals(_points(sp), 120);

    // SP -> DP, NP, itself, IP/SP
    rell.test.tx().op(
        transfer_action_points(system_provider.pub, dapp_provider2.pub, 10),
        transfer_action_points(system_provider.pub, node_provider2.pub, 10),
        transfer_action_points(system_provider.pub, system_provider.pub, 10),
        transfer_action_points(system_provider.pub, initial_provider.pub, 10)
    ).sign(system_provider).run();
    assert_equals(_points(dp2), 130);
    assert_equals(_points(np2), 76);
    assert_equals(_points(np1), 120);
    assert_equals(_points(initial_provider()), 103);

    // Insufficient amount
    rell.test.tx().op(
        transfer_action_points(system_provider.pub, dapp_provider2.pub, 100000)
    ).sign(system_provider).run_must_fail();

    // From non provider to provider
    rell.test.tx().op(
        transfer_action_points(x"ff", dapp_provider1.pub, 10)
    ).sign(system_provider).run_must_fail();
    // From provider to non provider
    rell.test.tx().op(
        transfer_action_points(system_provider.pub, x"ff", 10)
    ).sign(system_provider).run_must_fail();
}

function _points(provider?) = provider_rl_state @ { provider!! } ( .points );

function _enable_provider(provider, additional_voter: rell.test.keypair? = null) {
    rell.test.tx().op(
        propose_provider_state(initial_provider.pub, provider.pubkey, true)
    ).sign(initial_provider).run();

    if (additional_voter != null) {
        _provider_votes(additional_voter);
    }
}

function _provider_votes(voter: rell.test.keypair) {
    rell.test.tx().op(
        make_vote(voter.pub, last_proposal().rowid.to_integer(), true)
    ).sign(voter).run();
}
