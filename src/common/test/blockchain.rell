@test module;

import ^^.*;
import ^.util.*;
import ^^.operations.*;
import ^^.queries.*;
import ^^.test.ops.*;
import ^^.test.setup.*;
import proposal_blockchain.*;
import proposal_container.*;
import proposal_provider.*;

function test_add_and_remove_blockchain_replica() {
    setup_module();

    val node_provider1 = rell.test.keypairs.bob;
    val np1_node_key = x"bbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbb6D21B6E98F0CCE20A0310B7A5";

    rell.test.tx().op(
        propose_container(initial_provider.pub, clusters.system, "container1", voter_sets.system_p),
        propose_blockchain(initial_provider.pub, ["config_consensus_strategy": "HEADER_HASH".to_gtv()].to_gtv().to_bytes(),
            "foo_chain", "container1"),
        register_provider(initial_provider.pub, node_provider1.pub, provider_tier.NODE_PROVIDER),
        propose_provider_state(initial_provider.pub, node_provider1.pub, true)
    ).sign(initial_provider).run();

    rell.test.tx().op(
        register_node(node_provider1.pub, np1_node_key, "host", 7777, "http://host/api_url")
    ).sign(node_provider1).run();

    val np1_node = node @ { np1_node_key };
    val foo_chain = blockchain @ { "foo_chain" };

    // adding blockchain replica node
    rell.test.tx().op(
        add_blockchain_replica(node_provider1.pub, foo_chain.rid, np1_node_key)
    ).sign(node_provider1).run();
    // adding the same again, must fail
    rell.test.tx().op(
        add_blockchain_replica(node_provider1.pub, foo_chain.rid, np1_node_key)
    ).sign(node_provider1).run_must_fail();
    // asserts
    val db_replicas = blockchain_replica_node @* { foo_chain } (.node.pubkey);
    assert_equals(db_replicas, [np1_node_key]);
    val query_replicas = get_blockchain_replicas(foo_chain.rid);
    assert_equals(query_replicas.size(), 1);
    assert_equals(query_replicas[0][0], np1_node_key);

    // removing blockchain replica node
    rell.test.tx().op(
        remove_blockchain_replica(node_provider1.pub, foo_chain.rid, np1_node_key)
    ).sign(node_provider1).run();
    // asserts
    assert_true(empty(blockchain_replica_node @* { foo_chain } (.node.pubkey)));
    assert_true(empty(get_blockchain_replicas(foo_chain.rid)));

    // Testing requirement: Not my node
    rell.test.tx().op(
        add_blockchain_replica(initial_provider.pub, foo_chain.rid, np1_node_key),
        remove_blockchain_replica(initial_provider.pub, foo_chain.rid, np1_node_key)
    ).sign(initial_provider).run_must_fail();

    // Test that disabled node can't be added as bc replica
    rell.test.tx().op(
        disable_node(node_provider1.pub, np1_node_key)
    ).sign(node_provider1).run();

    rell.test.tx().op(
        rell.test.nop(),
        add_blockchain_replica(node_provider1.pub, foo_chain.rid, np1_node_key)
    ).sign(node_provider1).run_must_fail("Node not found or disabled");
}

function test_blockchain_dependencies() {
    setup_module(provider_quota_max_containers = 2);

    var fooConf = ["config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_container(initial_provider.pub, clusters.system, "container1", voter_sets.system_p),
        propose_blockchain(initial_provider.pub, fooConf.to_gtv().to_bytes(),
            "foo_chain", "container1")
    ).sign(initial_provider).run();
    val foo_chain = blockchain @ { "foo_chain" };

    val list = list<(text, byte_array)>();
    list.add(("", foo_chain.rid));
    var feeConf = ["config_consensus_strategy": "HEADER_HASH".to_gtv()];
    feeConf["dependencies"] = list.to_gtv();
    rell.test.tx().op(
        propose_blockchain(initial_provider.pub, feeConf.to_gtv().to_bytes(), "fee_chain", "container1")
    ).sign(initial_provider).run();

    val fooDependency = blockchain_dependency @ { .dependent_on == foo_chain, .height == 0 };
    val fee_chain = blockchain @ { "fee_chain" };
    assert_equals(fooDependency.me, fee_chain);

    // Assert that we can't remove chain with dependent chains
    rell.test.tx().op(
        propose_blockchain_action(initial_provider.pub, foo_chain.rid, blockchain_action.remove)
    ).sign(initial_provider).run_must_fail();
}

function test_blockchain_dependencies_only_allowed_in_same_container() {
    setup_module(provider_quota_max_containers = 3);

    var fooConf = ["config_consensus_strategy": "HEADER_HASH".to_gtv()];
    rell.test.tx().op(
        propose_container(initial_provider.pub, clusters.system, "container1", voter_sets.system_p),
        propose_blockchain(initial_provider.pub, fooConf.to_gtv().to_bytes(),
            "foo_chain", "container1")
    ).sign(initial_provider).run();
    val foo_chain = blockchain @ { "foo_chain" };

    val list = list<(text, byte_array)>();
    list.add(("", foo_chain.rid));
    var feeConf = ["config_consensus_strategy": "HEADER_HASH".to_gtv()];
    feeConf["dependencies"] = list.to_gtv();
    rell.test.tx().op(
        propose_container(initial_provider.pub, clusters.system, "container2", voter_sets.system_p),
        propose_blockchain(initial_provider.pub, feeConf.to_gtv().to_bytes(),
            "fee_chain", "container2")
    ).sign(initial_provider).run_must_fail();
}