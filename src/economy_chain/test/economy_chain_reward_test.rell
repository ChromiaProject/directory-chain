@test module;

import ^^.*;
import ^.helper_functions.*;

namespace ft4 {
    import lib.ft4.accounts;
    import lib.ft4.assets;
    import lib.ft4.auth;
    import test_utils: lib.ft4.test.utils;
}
import lib.auth;

struct module_args {
    auth_privkey: byte_array;
    admin_privkey: byte_array;
}

val auth_privkey = chain_context.args.auth_privkey;
val admin_privkey = chain_context.args.admin_privkey;
val provider_key = rell.test.keypairs.bob;
val provider_pubkey = provider_key.pub;
val node_pubkey = rell.test.pubkeys.charlie;
val system_node_pubkey = rell.test.pubkeys.alice;
val base_reward: big_integer = 1360015000; // SCU = 16, extra_storage = 1024, 5 CHR/USD
val system_node_reward: big_integer = 170000000; // system node reward = 34, 5 CHR/USD
val base_time = 10000000000000;
val cluster_name = "Bob's cluster";

function test_asset_units() {
    assert_equals(units_per_asset, 1000000);
}

function test_rewards_with_no_providers() {
    rell.test.tx().op(init()).run();

    add_assets_to_pool(20000000);
    assert_equals(get_pool_balance(), 20000000);

    rell.test.block().run();

    assert_equals(get_pool_balance(), 20000000);
}

function test_calculate_system_cluster_rewards_with_constants_0() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000);
}

function test_calculate_system_cluster_rewards_with_constants_0_availability_095() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 0.95]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000 * 0.5);
}

function test_calculate_system_cluster_rewards_with_system_provider_risk_share_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0.5, 0)
    ).sign(admin_privkey).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000 * 0.5);
}

function test_calculate_system_cluster_rewards_with_system_provider_fee_share_05_and_total_cost_system_providers_1() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 1, 0.5, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 10000000 * 13 * 0.5 );//todo
}

function test_calculate_dapp_cluster_rewards_with_constants_0() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_constants_0_availability_095() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 0.95]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_constants_0_occupancy_0() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 0, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_0() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0.5)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 0, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_1() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0.5)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0, 0, 0.5)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 6, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.75);
}

function test_calculate_dapp_cluster_rewards_with_system_provider_fee_share_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0.5, 0, 0, 0, 0,0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_staking_reward_fee_share_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0.5, 0, 0, 0,0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 2 / 3);
}

function test_calculate_dapp_cluster_rewards_with_chromia_foundation_fee_share_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0.5, 0, 0,0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_resource_pool_margin_fee_share_05() {
    rell.test.tx().op(
        update_economy_constants(1, 1, 10, 100, 0, 0, 0, 0.5, 0,0)
    ).sign(admin_privkey).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_pay_rewards_dapp_cluster_full_reward_is_payed() {
    val pa = run_pay_rewards(33090909, false);

    assert_equals(get_account_balance(pa.account), 33090909);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 33090909);
}

function test_pay_rewards_dapp_cluster_empty_pool_balance() {

    val pa = run_pay_rewards(0, false);

    assert_equals(get_account_balance(pa.account), 0);
    assert_equals(pa.reward_debt, 33090909);
    assert_equals(get_pool_balance(), 0);
}

function test_pay_rewards_dapp_cluster_not_enough_funds() {

    val pa = run_pay_rewards(1, false);

    assert_equals(get_account_balance(pa.account), 1);
    assert_equals(pa.reward_debt, 33090909 - 1);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 1);
}

function test_pay_reward_to_system_cluster() {

    val pa = run_pay_rewards(3726650000, true);

    assert_equals(get_account_balance(pa.account), 3726650000);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_pool_balance(), 0);
}

///////////////////
// Helper functions

function run_calculate_reward(node_availability_report: node_availability_report, create_lease_number: integer, calculate_system_reward: boolean) {

    rell.test.tx().op(init()).run();

    val system_cluster = ensure_system_cluster();
    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(cluster_name, node_pubkey, false);
    val system_node = ensure_node(provider_pubkey, system_node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(system_cluster_name, system_node_pubkey, false);

    val alice = require(do_create_account(rell.test.keypairs.alice, auth_privkey));

    if (create_lease_number > 0) {
        rell.test.tx().op(
            create_lease("lease", create_lease_number, alice, cluster, false)
        ).run();
    }

    if (calculate_system_reward) {
        return calculate_system_cluster_rewards(system_cluster, node_availability_report)[system_node];
    } else {
        return calculate_dapp_cluster_rewards(cluster, node_availability_report)[node];
    }
}

function run_pay_rewards(pool_assets: big_integer = 0, calculate_system_reward: boolean) {

    rell.test.tx().op(init()).run();

    val system_cluster = ensure_system_cluster();
    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val calculate_cluster = if (calculate_system_reward) system_cluster else cluster;
    add_cluster_node(calculate_cluster.name, node_pubkey, false);

    val alice = require(do_create_account(rell.test.keypairs.alice, auth_privkey));
    if (pool_assets != 0) {
        add_assets_to_pool(pool_assets);
    }

    rell.test.tx().op(
        register_provider_account(provider_pubkey),
        pay_rewards_op(calculate_cluster, node_availability_report)
    ).sign(provider_key).run();

    val pa = provider_account @ { provider @ { provider_pubkey } };
    return pa;
}
