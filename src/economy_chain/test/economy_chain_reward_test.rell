@test module;

import ^^.*;
import ^.helper_functions.*;

namespace ft4 {
    import lib.ft4.accounts;
    import lib.ft4.assets;
    import lib.ft4.auth;
    import test_utils: lib.ft4.test.utils;
}
import lib.auth;

struct module_args {
    auth_privkey: byte_array;
    admin_privkey: byte_array;
    staking_rate_change_delay_ms: integer;
}

val auth_privkey = chain_context.args.auth_privkey;
val admin_privkey = chain_context.args.admin_privkey;
val staking_rate_change_delay_ms = chain_context.args.staking_rate_change_delay_ms;
val provider_key = rell.test.keypairs.bob;
val provider_pubkey = provider_key.pub;
val node_pubkey = rell.test.pubkeys.charlie;
val system_node_pubkey = rell.test.pubkeys.alice;
val base_reward: big_integer = 1360015000; // SCU = 16, extra_storage = 1024, 5 CHR/USD
val system_node_reward: big_integer = 170000000; // system node reward = 34, 5 CHR/USD
val base_time = 10000000000000;
val cluster_name = "Bob's cluster";

function test_asset_units() {
    assert_equals(units_per_asset, 1000000);
}

function test_rewards_with_no_providers() {
    rell.test.tx().op(init()).run();

    add_assets_to_pool(20000000);
    assert_equals(get_pool_balance(), 20000000);

    rell.test.block().run();

    assert_equals(get_pool_balance(), 20000000);
}

function test_calculate_system_cluster_rewards_with_constants_0() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000);
}

function test_calculate_system_cluster_rewards_with_constants_0_availability_095() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 0.95]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000 * 0.5);
}

function test_calculate_system_cluster_rewards_with_system_provider_risk_share_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0.5),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    assert_equals(reward, 1000000000 * 0.5);
}

function test_calculate_system_cluster_rewards_with_system_provider_fee_share_05_and_total_cost_system_providers_1() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(1, 0.5, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [system_node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, true);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 10000000 * 13 * 0.5 );
}

function test_calculate_dapp_cluster_rewards_with_constants_0() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_constants_0_availability_095() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 0.95]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_constants_0_occupancy_0() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 0, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_0() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0.5)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 0, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_1() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0.5)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000);
}

function test_calculate_dapp_cluster_rewards_with_dapp_provider_risk_share_05_occupancy_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0.5)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 6, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.75);
}

function test_calculate_dapp_cluster_rewards_with_system_provider_fee_share_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0.5, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_staking_reward_fee_share_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0.5, 0, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 2 / 3);
}

function test_calculate_dapp_cluster_rewards_with_chromia_foundation_fee_share_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0.5, 0, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_calculate_dapp_cluster_rewards_with_resource_pool_margin_fee_share_05() {
    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_system_provider_economy_constants(100, 0, 0),
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();
    rell.test.tx().op(
        update_economy_constants(1, 1, 0, 0, 0.5, 0)
    ).sign(rell.test.keypairs.alice).run();

    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val reward = run_calculate_reward(node_availability_report, 12, false);

    // 12 scu priced 1 plus 1 gb extra storage priced 1
    assert_equals(reward, 130000000 * 0.5);
}

function test_pay_rewards_dapp_cluster_full_reward_is_payed() {
    val pa = run_pay_rewards(33090909, false);

    assert_equals(get_account_balance(pa.account), 33090909);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 33090909);
}

function test_pay_rewards_dapp_cluster_empty_pool_balance() {

    val pa = run_pay_rewards(0, false);

    assert_equals(get_account_balance(pa.account), 0);
    assert_equals(pa.reward_debt, 33090909);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 0);
}

function test_pay_rewards_dapp_cluster_not_enough_funds() {

    val pa = run_pay_rewards(1, false);

    assert_equals(get_account_balance(pa.account), 1);
    assert_equals(pa.reward_debt, 33090909 - 1);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 1);
}

function test_pay_rewards_dapp_cluster_pay_full_debt() {

    val pa = run_pay_rewards(33090909 + 100, false, 90);

    assert_equals(get_account_balance(pa.account), 33090909 + 90);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_pool_balance(), 10);
    assert_equals(node_payed_reward @ {} ( .value), 33090909 + 90);
}

function test_pay_rewards_dapp_cluster_pay_partialy_debt() {

    val pa = run_pay_rewards(33090909 + 90, false, 100);

    assert_equals(get_account_balance(pa.account), 33090909 + 90);
    assert_equals(pa.reward_debt, 10);
    assert_equals(get_pool_balance(), 0);
    assert_equals(node_payed_reward @ {} ( .value), 33090909 + 90);
}

function test_pay_reward_to_system_cluster() {

    val pa = run_pay_rewards(3726650000, true);

    val chromia_foundation_account = get_chromia_foundation_account();

    assert_equals(get_account_balance(pa.account), 3726650000);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_account_balance(chromia_foundation_account), 0);
    assert_equals(chromia_foundation.reward_debt, 6500000);
    assert_equals(get_pool_balance(), 0);
}

function test_pay_reward_to_chromia_foundation() {

    val pa = run_pay_rewards(3726650000 + 500000, true);

    val chromia_foundation_account = get_chromia_foundation_account();

    assert_equals(get_account_balance(pa.account), 3726650000);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_account_balance(chromia_foundation_account), 500000);
    assert_equals(chromia_foundation.reward_debt, 6000000);
    assert_equals(get_pool_balance(), 0);
}

function test_pay_reward_new_account_and_empty_pool() {

    val alice = run_pay_reward(1000, false);

    assert_equals(get_account_balance(alice), 1000000000);
    assert_equals(get_pool_balance(), 0);
}

function test_pay_reward_mint_pool_account() {
    rell.test.set_next_block_time_delta(86400001);
    rell.test.block().run();

    val alice = run_pay_reward(1000, true);

    assert_equals(get_account_balance(alice), 1000001000);
    assert_equals(get_pool_balance(), 999999000);
}

function test_calculate_evm_transactions_submitters_rewards() {
    rell.test.tx().op(init()).run();

    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(cluster_name, node_pubkey, false);

    rell.test.tx().op(
        create_price_oracle_rates_op("tCHR", "Chromia Test", 1.00),
        create_price_oracle_rates_op("ETH", "Ether", 2.00),
        create_price_oracle_rates_op("BNB", "Binance smart chain", 10.00),
        create_price_oracle_rates_op("MATIC", "Polygon", 0.01)
    ).run();

    val result1 = calculate_evm_transaction_submitter_reward(evm_transaction_submitter_cost(id = x"01", processed_by = node_pubkey, cost  = 10000L, currency_symbol = "ETH"));
    val result2 = calculate_evm_transaction_submitter_reward(evm_transaction_submitter_cost(id = x"02", processed_by = node_pubkey, cost  = 10000L, currency_symbol = "BNB"));
    val result3 = calculate_evm_transaction_submitter_reward(evm_transaction_submitter_cost(id = x"03", processed_by = node_pubkey, cost  = 10000L, currency_symbol = "MATIC"));

    assert_equals(result1[node], 22000);
    assert_equals(result2[node], 110000);
    assert_equals(result3[node], 110);
}

function test_receive_evm_transaction_submitter_cost_message() {
    rell.test.tx().op(init()).run();
    update_staking_requirements_constants(staking_requirements_enabled = false);

    val evm_transaction_submitter_bc_rid = x"1111";

    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(cluster_name, node_pubkey, false);

    rell.test.tx().op(
        register_provider_account_op(provider)
    ).sign(provider_key).run();

    rell.test.tx().op(
        create_price_oracle_rates_op("tCHR", "Chromia Test", 1.00),
        create_price_oracle_rates_op("ETH", "Ether", 2.00)
    ).run();

    rell.test.tx().op(
        icmf_message_op(x"", blockchain_rid_topic, body = blockchain_rid(
            rid = evm_transaction_submitter_bc_rid,
            name = "evm_transaction_submitter_chain").to_gtv())
    ).run();

    val expected_sender = sender_blockchain @ {};
    assert_equals(expected_sender.rid, evm_transaction_submitter_bc_rid);
    assert_equals(expected_sender.name, "evm_transaction_submitter_chain");

    rell.test.tx().op(
        icmf_message_op(evm_transaction_submitter_bc_rid, evm_transaction_submitter_cost_topic, body = evm_transaction_submitter_cost(
            id = x"10",
            processed_by = node_pubkey,
            cost = 6L,
            currency_symbol = "ETH"
        ).to_gtv())
    ).run();

    assert_equals(node_payed_reward @ {} ( .value), 13);
}

function test_receive_evm_transaction_submitter_cost_invalid_sender() {
    rell.test.tx().op(init()).run();

    rell.test.tx().op(
        icmf_message_op(x"", evm_transaction_submitter_cost_topic, body = evm_transaction_submitter_cost(
            id = x"10",
            processed_by = node_pubkey,
            cost = 6L,
            currency_symbol = "ETH"
        ).to_gtv())
    ).run();
    assert_true(node_payed_reward @? {} == null);
}

function test_pay_shared_rewards_dapp_cluster_full_reward_is_payed() {
    rell.test.tx().op(init()).run();

    val test_account_dave = require(do_create_account(rell.test.keypairs.dave, auth_privkey));
    val test_account_bob = require(do_create_account(rell.test.keypairs.bob, auth_privkey));
    val test_account_charlie = require(do_create_account(rell.test.keypairs.charlie, auth_privkey));

    rell.test.tx()
        .op(staking_set_rate(0.5))
        .op(mint_to_pool(8888888888))
        .op(mint_to(test_account_dave, 9999999))
        .op(mint_to(test_account_bob, 9999999))
    .sign(admin_privkey).run();

    add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);

    do_staking_deposit_native(rell.test.keypairs.dave, 9999999);
    do_staking_delegate(rell.test.keypairs.dave, provider_pubkey);

    assert_equals(get_account_balance(test_account_dave), 1000000000);

    // FFWD
    rell.test.set_next_block_time_delta(staking_rate_change_delay_ms);
    rell.test.block().run(); rell.test.block().run();

    assert_equals(staking_get_current_delegate(test_account_dave.id), provider_pubkey);

    val pa = run_pay_rewards(33090909, true);

    assert_equals(get_account_balance(pa.account), 3726650000);
    assert_equals(pa.reward_debt, 0);
    assert_equals(get_account_balance(test_account_dave), 1000000000 + (0.1 * 3726650000));
}

function test_staking_req_reward_fail_due_to_low_own() {

    update_staking_requirements_constants(
        staking_requirement_system_provider_own_stake_usd = 100000000000
    );

    val pa = run_staking_pay_out();

    assert_equals(get_account_balance(pa.account), 0);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, 0);
}

function test_staking_req_reward_fail_due_to_low_total() {

    update_staking_requirements_constants(
        staking_requirement_system_provider_total_stake_usd = 100000000000
    );

    val pa = run_staking_pay_out();

    assert_equals(get_account_balance(pa.account), 0);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, 0);
}

function test_staking_req_reward_successful() {

    val pa = run_staking_pay_out(
        reward = 10,
        staking_amount = (economy_constants.staking_requirement_system_provider_total_stake_usd / economy_constants.chr_per_usd).to_integer() + 100
    );

    assert_equals(get_account_balance(pa.account), 11);
    assert_equals(pa.reward_debt, 0);
    assert_gt(pa.last_approved_stake_requirements, 0);
}

function test_staking_req_reward_successful_due_to_disabled() {

    update_staking_requirements_constants(staking_requirements_enabled = false);

    val pa = run_staking_pay_out(
        staking_amount = 0
    );

    assert_equals(get_account_balance(pa.account), 10);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, 0);
}

function test_staking_req_reward_successful_followed_by_stopped() {

    // Staking OK - pay reward
    update_staking_requirements_constants(
        staking_requirements_enabled = true,
        staking_requirement_system_provider_own_stake_usd = 0,
        staking_requirement_system_provider_total_stake_usd = 0,
        staking_requirement_dapp_provider_own_stake_usd = 1,
        staking_requirement_dapp_provider_total_stake_usd = 0
    );

    var pa = run_staking_pay_out(
        reward = 10,
        staking_amount = 10
    );

    assert_equals(get_account_balance(pa.account), 11);
    assert_equals(pa.reward_debt, 0);
    assert_gt(pa.last_approved_stake_requirements, 0);

    val node = node @ { node_pubkey };
    var last_approved_stake_requirements = pa.last_approved_stake_requirements;
    val node_reward = map<node, big_integer>();
    node_reward[node] = 10;

    // Staking not OK - pay reward since number of days not yet reached for a stop
    update_staking_requirements_constants(
        staking_requirement_dapp_provider_total_stake_usd = 1000000
    );
    rell.test.set_next_block_time_delta((economy_constants.staking_requirement_stop_payout_days - 1) * DAY_MS); // Time is limit-1 day
    rell.test.block().run();

    rell.test.tx().op(
        pay_out_node_rewards_op(node_reward)
    ).nop().sign(provider_key).run();

    pa = provider_account @ { provider @ { provider_pubkey } };

    assert_equals(get_account_balance(pa.account), 22);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, last_approved_stake_requirements);

    // Staking not OK - do not pay reward since number of days passed
    rell.test.set_next_block_time_delta(2 * DAY_MS); // + 2 days (days limit passed)
    rell.test.block().run();

    node_reward[node] = 20;
    rell.test.tx().op(
        pay_out_node_rewards_op(node_reward)
    ).sign(provider_key).run();

    pa = provider_account @ { provider @ { provider_pubkey } };

    assert_equals(get_account_balance(pa.account), 22);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, last_approved_stake_requirements);
}

///////////////////
// Helper functions

function run_staking_pay_out(
    reward: integer = 10,
    staking_amount: integer = 0
) {

    if (not state.initialized) rell.test.tx().op(init()).run();

    val system_cluster = ensure_system_cluster();
    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(cluster_name, node_pubkey, false);
    val system_node = ensure_node(provider_pubkey, system_node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(system_cluster_name, system_node_pubkey, false);

    rell.test.tx().op(
        register_provider_account_op(provider)
    ).sign(provider_key).run();

    val pa = provider_account @ { provider @ { provider_pubkey } };
    assert_equals(get_account_balance(pa.account), 0);
    assert_equals(pa.reward_debt, 0);
    assert_equals(pa.last_approved_stake_requirements, 0);

    val node_reward = map<node, big_integer>();
    node_reward[node] = reward;
    rell.test.tx().op(
        add_staking(pa, staking_amount)
    ).sign(provider_key).run();

    rell.test.set_next_block_time_delta(chain_context.args.staking_rate_change_delay_ms);
    rell.test.block().run();

    rell.test.tx().op(
        pay_out_node_rewards_op(node_reward)
    ).sign(provider_key).run();

    return provider_account @ { provider @ { provider_pubkey } };
}

function run_calculate_reward(node_availability_report: node_availability_report, create_lease_number: integer, calculate_system_reward: boolean) {

    if (not state.initialized) rell.test.tx().op(init()).run();

    val system_cluster = ensure_system_cluster();
    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(cluster_name, node_pubkey, false);
    val system_node = ensure_node(provider_pubkey, system_node_pubkey, true, "SE", 1, 1024, false);
    add_cluster_node(system_cluster_name, system_node_pubkey, false);

    val alice = require(do_create_account(rell.test.keypairs.alice, auth_privkey));

    if (create_lease_number > 0) {
        rell.test.tx().op(
            create_lease("lease", create_lease_number, alice, cluster, false)
        ).run();
    }

    if (calculate_system_reward) {
        return calculate_system_cluster_rewards(system_cluster, node_availability_report)[system_node];
    } else {
        return calculate_dapp_cluster_rewards(cluster, node_availability_report)[node];
    }
}

function run_pay_rewards(pool_assets: big_integer = 0, calculate_system_reward: boolean, reward_debt: big_integer = 0) {

    if (not state.initialized) rell.test.tx().op(init()).run();

    update_staking_requirements_constants(staking_requirements_enabled = false);

    val system_cluster = ensure_system_cluster();
    val cluster = ensure_cluster(cluster_name, false, false, 1, 1024);
    val provider = add_provider(provider_pubkey, false, provider_tier.NODE_PROVIDER);
    val node = ensure_node(provider_pubkey, node_pubkey, true, "SE", 1, 1024, false);
    val node_availability_report = node_availability_report(start_time = 0, end_time = 0, [node_pubkey: 1.0]);

    val calculate_cluster = if (calculate_system_reward) system_cluster else cluster;
    add_cluster_node(calculate_cluster.name, node_pubkey, false);

    rell.test.tx().op(
        set_pool_details_last_refill_millis_last_block_time_op()
    ).run();

    val alice = require(do_create_account(rell.test.keypairs.alice, auth_privkey));
    if (pool_assets != 0) {
        add_assets_to_pool(pool_assets);
    }

    rell.test.tx().op(
        register_provider_account_op(provider),
        set_reward_debt_op(provider_pubkey, reward_debt),
        pay_rewards_op(calculate_cluster, node_availability_report)
    ).sign(provider_key).run();

    val pa = provider_account @ { provider @ { provider_pubkey } };
    return pa;
}

function run_pay_reward(reward: big_integer = 0, refill: boolean) {

    if (not state.initialized) rell.test.tx().op(init()).run();

    val pool = get_pool_account();
    var pool_balance = get_account_balance(pool);

    val alice = require(do_create_account(rell.test.keypairs.alice, auth_privkey));

    if (not refill) {
        rell.test.tx().op(
            set_pool_details_last_refill_millis_last_block_time_op()
        ).run();
    }

    rell.test.tx().op(
        pay_reward_op(pool, pool_balance, alice, 0, reward, "test")
    ).run();

    return alice;
}

function update_staking_requirements_constants(
    staking_requirements_enabled: boolean? = null,
    staking_requirement_stop_payout_days: integer? = null,
    staking_requirement_system_provider_own_stake_usd: integer? = null,
    staking_requirement_system_provider_total_stake_usd: integer? = null,
    staking_requirement_dapp_provider_own_stake_usd: integer? = null,
    staking_requirement_dapp_provider_total_stake_usd: integer? = null
) {

    rell.test.tx().op(
        update_staking_requirements_economy_constants(
            staking_requirements_enabled,
            staking_requirement_stop_payout_days,
            staking_requirement_system_provider_own_stake_usd,
            staking_requirement_system_provider_total_stake_usd,
            staking_requirement_dapp_provider_own_stake_usd,
            staking_requirement_dapp_provider_total_stake_usd
        )
    ).sign(admin_privkey).run();
}
