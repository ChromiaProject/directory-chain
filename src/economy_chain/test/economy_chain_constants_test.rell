@test module;

import ^^.*;
import ^.helper_functions.*;

struct module_args {
    admin_privkey: byte_array;
}

val admin_privkey = chain_context.args.admin_privkey;

function test_update_chr_per_usd() {
    val constants: economy_constants_data = get_economy_constants();
    assert_equals(constants.chr_per_usd, 5);

    rell.test.tx().op(
        update_chr_per_usd(9)
    ).run_must_fail("Expected admin signature");

    rell.test.tx().op(
        update_chr_per_usd(10)
    ).sign(admin_privkey).run();

    val updated_constants: economy_constants_data = get_economy_constants();
    assert_equals(updated_constants.chr_per_usd, 10);
}

function test_update_system_provider_economy_constants() {
    val constants: economy_constants_data = get_economy_constants();
    assert_equals(constants.total_cost_system_providers, 828);
    assert_equals(constants.system_provider_fee_share, 0.1);
    assert_equals(constants.system_provider_risk_share, 0.1);

    rell.test.tx().op(
        update_system_provider_economy_constants(1, 2, 3)
    ).run_must_fail("Expected admin signature");

    rell.test.tx().op(
        update_system_provider_economy_constants(2000, 0.2, 0.3)
    ).sign(admin_privkey).run();

    val updated_constants: economy_constants_data = get_economy_constants();
    assert_equals(updated_constants.total_cost_system_providers, 2000);
    assert_equals(updated_constants.system_provider_fee_share, 0.2);
    assert_equals(updated_constants.system_provider_risk_share, 0.3);
}

function test_update_staking_requirements_economy_constants() {

    rell.test.tx().op(
        update_staking_requirements_economy_constants(false, 1, 2, 3, 4, 5)
    ).run_must_fail("Expected admin signature");

    rell.test.tx().op(
        update_staking_requirements_economy_constants(false, 1, 2, 3, 4, 5)
    ).sign(admin_privkey).run();

    val updated_constants: economy_constants_data = get_economy_constants();
    assert_equals(updated_constants.staking_requirements_enabled, false);
    assert_equals(updated_constants.staking_requirement_stop_payout_days, 1);
    assert_equals(updated_constants.staking_requirement_system_provider_own_stake_usd, 2);
    assert_equals(updated_constants.staking_requirement_system_provider_total_stake_usd, 3);
    assert_equals(updated_constants.staking_requirement_dapp_provider_own_stake_usd, 4);
    assert_equals(updated_constants.staking_requirement_dapp_provider_total_stake_usd, 5);
}

function test_update_economy_constants() {
    val constants: economy_constants_data = get_economy_constants();
    assert_equals(constants.min_lease_time_weeks, 1);
    assert_equals(constants.max_lease_time_weeks, 12);
    assert_equals(constants.chr_per_usd, 5);
    assert_equals(constants.total_cost_system_providers, 828);
    assert_equals(constants.system_provider_fee_share, 0.1);
    assert_equals(constants.staking_reward_fee_share, 0.1);
    assert_equals(constants.chromia_foundation_fee_share, 0.1);
    assert_equals(constants.resource_pool_margin_fee_share, 0.1);
    assert_equals(constants.system_provider_risk_share, 0.1);
    assert_equals(constants.dapp_provider_risk_share, 0.2);

    // Update constants
    rell.test.tx().op(
        update_economy_constants(1, 2, 3, 4, 5, 6)
    ).run_must_fail("Expected system provider signature");

    add_provider(rell.test.keypairs.alice.pub, true, provider_tier.NODE_PROVIDER);

    rell.test.tx().op(
        update_economy_constants(1, 2, 3, 4, 5, 6)
    ).sign(rell.test.keypairs.alice).run();

    val updated_constants: economy_constants_data = get_economy_constants();
    assert_equals(updated_constants.min_lease_time_weeks, 1);
    assert_equals(updated_constants.max_lease_time_weeks, 2);
    assert_equals(updated_constants.chr_per_usd, 5); // Unchanged
    assert_equals(updated_constants.total_cost_system_providers, 828); // Unchanged
    assert_equals(updated_constants.system_provider_fee_share, 0.1); // Unchanged
    assert_equals(updated_constants.staking_reward_fee_share, 3);
    assert_equals(updated_constants.chromia_foundation_fee_share, 4);
    assert_equals(updated_constants.resource_pool_margin_fee_share, 5);
    assert_equals(updated_constants.system_provider_risk_share, 0.1); // Unchanged
    assert_equals(updated_constants.dapp_provider_risk_share, 6);
}