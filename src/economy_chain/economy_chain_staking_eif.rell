val STAKING_NETWORK_ID_BSC = 56;
val STAKING_NETWORK_ID_ETH = 1;
val STAKING_NETWORK_ID_BSC_TEST = 97;

entity provisional_stake_balance {
    key address: byte_array, network;
    mutable staked_balance: integer;
}

struct event_data {
    tnx_hash: byte_array;
    log_index: integer;
    signature: byte_array;
    contract_address: byte_array;
    name: text;
    indexed_values: list<gtv>;
    non_indexed_values: list<gtv>;
}

function _staking_import_evm_balance_impl(address: byte_array, network_id: integer) {
    require_initialized();

    val staker = ft4.auth.authenticate();
    val account = require(
        eif
            .hbridge
            .find_ft4_account_for_address(
                address,
                network_id
            ),
        "No account link established for this address."
    );
    require(account == staker, "Address does not belong to the authenticated account.");

    flush_provisional_stake_balance(staker, address);
}

function _staking_network_id_to_enum(network_id: integer) = when (network_id) {
    STAKING_NETWORK_ID_ETH -> network.ETH;
    STAKING_NETWORK_ID_BSC -> network.BSC;
    STAKING_NETWORK_ID_BSC_TEST -> network.BSC;
    else -> network.CHR;
};

function flush_provisional_stake_balance(account: ft4.accounts.account, address: byte_array) {
    val provisional = provisional_stake_balance @* { address };
    for (p in provisional) {
        _staking_update_evm_state(account, p.network, p.staked_balance);
        delete p;
    }
}

function __staking_evm_block(network_id: integer, evm_block_height: integer, evm_block_hash: byte_array, events: list<event_data>) {
    for (event in events) {
        val evm_address = byte_array.from_gtv(event.indexed_values[0]);

        val current_network = _staking_network_id_to_enum(network_id);
        if (current_network == network.CHR) {
            log("Unknown staking network id: ", network_id);
            return;
        }

        val ft4_account = eif.hbridge.find_ft4_account_for_address(evm_address, network_id);

        if (event.name == "WithdrawRequest") {
            if (exists(ft4_account)) {
                _staking_request_withdrawal(ft4_account, current_network);
            } else {
                delete provisional_stake_balance @? { evm_address, current_network };
            }
        } else if (event.name == "StakeUpdate") {
            val staked_balance = integer.from_gtv(event.non_indexed_values[0]);

            if (exists(ft4_account)) {
                _staking_update_evm_state(ft4_account, current_network, staked_balance);
            } else {
                val provisional = provisional_stake_balance @? { evm_address, current_network };
                if (exists(provisional)) {
                    provisional.staked_balance = staked_balance;
                } else {
                    create provisional_stake_balance (
                        .address = evm_address,
                        .network = current_network,
                        .staked_balance = staked_balance
                    );
                }
            }
        } else {
            log("Unknown EIF event: ", event.name);
        }
    }
}
