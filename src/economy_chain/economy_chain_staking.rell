namespace ft4 {
    import lib.ft4.accounts;
    import lib.ft4.assets;
    import lib.ft4.auth;
}
import lib.auth;

val MS_PER_YEAR: decimal = 365 * 3600 * 1000;

entity staker_withdrawal_request {
    staker_withdrawal_request_time: timestamp;
    staker: ft4.accounts.account;
    mutable used: boolean;
    index staker;
    key staker_withdrawal_request_time, staker;
}

// NOTE: We need to keep a timeline as registrations do not take effect immediately
entity provider_registration {
    provider_registration_time: timestamp;
    provider: ft4.accounts.account;
    name: text;
    mutable active: boolean;
    index provider;
    key provider_registration_time, provider;
}

entity staker_payout {
    staker_payout_time: timestamp;
    staker: ft4.accounts.account;
    payout_amount: integer;
    mutable latest: boolean;
    key staker, staker_payout_time;
    index staker;
}

entity staker_state {
    staker_state_time: timestamp;
    previous_staker_state_time: timestamp; // We have no look-back windowing functions
    staker: ft4.accounts.account;
    cutover_time: timestamp;
    new_provider: ft4.accounts.account;
    old_provider: ft4.accounts.account;
    balance: integer;
    previous_balance: integer; // We have no look-back windowing functions
    mutable latest: boolean; // We have no windowing functions, and want to group by other fields
    index staker;
}

entity reward_rate {
    reward_rate_time: timestamp;
    delegator_rate: decimal;
    provider_rate: decimal;
    key reward_rate_time;
}

object staking_config {
    rate_change_delay_ms: integer = 7 * 8600 * 1000;
    withdrawal_delay_ms: integer = 14 * 8600 * 1000;
    // TODO Should have an independent setting for delegation delay
}

operation staking_set_rates_at(delegator_rate: decimal, provider_rate: decimal, time: timestamp) {
   _staking_set_rates_at(delegator_rate, provider_rate, time);
}

function _staking_set_rates_at(delegator_rate: decimal, provider_rate: decimal, time: timestamp) {
    require(time >= _staking_cannot_change_before(), "Cannot change rate before " + _staking_cannot_change_before());
    create reward_rate(time, delegator_rate, provider_rate);
}

operation staking_set_rates(delegator_rate: decimal, provider_rate: decimal) {
   _staking_set_rates_at(delegator_rate, provider_rate, _staking_cannot_change_before());
}

operation staking_deposit(staker: ft4.accounts.account, amount: integer, delegateTo: ft4.accounts.account? = null) {
    require(amount > 0, "Can't deposit nothing (or less)!");
    val existing = staker_state @? { .staker == staker and .latest };
    update staker_state @? { .staker == staker and .latest } ( latest = false );
    if (null != existing)
        create staker_state(
            staker_state_time = _staking_last_known_time(),
            previous_staker_state_time = existing.staker_state_time,
            staker = staker,
            balance = amount + existing.balance,
            previous_balance = existing.balance,
            cutover_time = existing.cutover_time,
            new_provider = existing.new_provider,
            old_provider = existing.old_provider,
            latest = true
        );
    else
        // First deposit
        create staker_state(
            staker_state_time = _staking_last_known_time(),
            previous_staker_state_time = 0,
            staker = staker,
            balance = amount,
            previous_balance = 0,
            cutover_time = _staking_last_known_time(),
            new_provider = get_null_account(),
            old_provider = get_null_account(),
            latest = true
        );
    ;

    if (null != delegateTo) {
        // Below check is duplicated, but less overhead than 2 wrapping functions.
        require(staking_is_active_provider(delegateTo), "Must be active provider.");
        _staking_delegate(staker, delegateTo);
    }
}

query staking_get_balance(staker: ft4.accounts.account) = staker_state @? { .staker == staker and .latest==true } ( .balance ) ?: 0;

operation staking_request_withdrawal(staker: ft4.accounts.account) {
    require(staking_get_balance(staker) > 0, "Need something to withdraw.");
    require(get_null_account() == staking_get_current_delegate(staker), "Must unstaker before requesting a withdrawal.");
    require(null == staking_get_withdrawal_time(staker), "Only one withdrawal request at a time.");

    // Make a new request
    require(_staking_last_known_time() > 0, "Require an actual time");
    create staker_withdrawal_request(staker_withdrawal_request_time=_staking_last_known_time(), staker=staker, false);
}

operation staking_withdraw(staker: ft4.accounts.account) {
    val withdrawal_time = staking_get_withdrawal_time(staker);
    require(withdrawal_time, "Need a withdrawal request.");
    require(_staking_last_known_time() >= withdrawal_time, "Need to wait for withdrawal request.");

    // TODO Send the balance back to them through ft4 somehow

    val existing = staker_state @ { .staker == staker and .latest==true };
    update staker_state @? { .staker == staker and .latest==true } ( latest=false );
    create staker_state(
        staker_state_time =_staking_last_known_time(),
        previous_staker_state_time = existing.staker_state_time,
        staker = staker,
        balance = 0,
        previous_balance = existing.balance,
        cutover_time = existing.cutover_time,
        new_provider = existing.new_provider,
        old_provider = existing.old_provider,
        latest = true
    );
    update staker_withdrawal_request @ {.staker == staker } (used = true );
}

query staking_get_withdrawal_time(staker: ft4.accounts.account): timestamp? {
    val valid_request = staker_withdrawal_request @? {.staker == staker and not .used};
    return
        if (null != valid_request)
            valid_request.staker_withdrawal_request_time + staking_config.withdrawal_delay_ms
        else
            null
        ;
}

query staking_is_active_provider(provider: ft4.accounts.account): boolean {
    val provider_registration = provider_registration @? {.provider == provider} (.active, @omit @sort_desc .rowid) limit 1;
    return provider_registration ?: false;
}

query staking_get_total_delegation_to(provider: ft4.accounts.account): integer {
    return
        // Look at the full staker state 
        (s: staker_state) @ {
            // consider only the latest state
            s.latest
            // restricted to those currently on this provider according to the cut off date
            and (
                ( s.new_provider == provider and s.cutover_time <= _staking_last_known_time() )
                or
                ( s.old_provider == provider and s.cutover_time > _staking_last_known_time() )
            )
        } (
            // sum the balances
            @sum s.balance
        )
    ;
}

query staking_rewards_claimable_for(staker: ft4.accounts.account): integer {

    // We are going to limit ourselves here by simply calculating the reward as of the last payout date at the
    // prevailing reward rate. This will give a "wrong" answer as a portion of the reward will be paid at the
    // new rate when it should have been on the old. This will not be significant.
    // The author would like to have done something more fun here tbh. Hopefully this will get easier as Rell
    // approaches its final form of a modern Tutorial D :)
    // TODO extend this query to handle the above accurately

    // So what we will do is consider all of the states, but we will cap the previous and current stake times
    // at the last payout date, which will cause periods enterly before the payout date to collapse to zero
    // duration, and the state during which the payout happened to be truncated to the payout date.
    // Each row also potentially needs to be cut in two, as the delegate could change to NO_ONE during it, we do
    // this by first considering the time before the cutoff date, and then the time afterwards (if any). 
    // We also need to recognise the last row, and for this row only, include the *present* balance extended to
    // the present date. In effect a last virtual row. (If we could UPSERT, we could cheaply create this row rather
    // than interpolating it.)

    val last_payout_time = (staker_payout @? {.staker == staker} (@max .staker_payout_time)) ?: 0;

    val total_token_time = staker_state @ {
        .staker == staker
    } (
        @sum(
            // Real and concluded states truncated by the last payment date
            (
                (
                    max(.staker_state_time, last_payout_time)
                        - max(.previous_staker_state_time, last_payout_time)
                ) * .previous_balance
            )
            // TODO We need to consider the cutoff time
            +
            // Interpolated last row from latest row
            (
                when (.latest) {
                    true -> (_staking_last_known_time() - .staker_state_time) * .balance;
                    else -> 0;
                }
            )
        )
    );

    // TODO Pick a separate rate for providers
    val rate = staking_get_current_rates().delegator_rate / MS_PER_YEAR;

    val token_reward = total_token_time * rate;
    return integer(token_reward); // Can only transfer whole minor units
}

operation staking_add_provider(name: text, provider: ft4.accounts.account) {
    require(not staking_is_active_provider(provider), "Provider cannot be active.");
    create provider_registration(_staking_last_known_time(), provider, name, true);
}

operation staking_remove_provider(provider: ft4.accounts.account) {
    require(staking_is_active_provider(provider), "Provider not active.");

    // Unwind delegations immediately
    // Find all the latest delegations involving removed provider
    // eg. latest AND new_provider is going
    val removed_delegations = staker_state @* { .new_provider == provider and .latest } (@group .staker);
    // then for each :-
    for (removed_delegation in removed_delegations) {
        val existing = staker_state @ {.latest and .staker == removed_delegation};
        // clear the latest flag
        update staker_state @ {.latest and .staker == removed_delegation} (.latest = false);
        // ... add an immediate transition to the prior
        create staker_state(
            staker = removed_delegation,
            cutover_time = staking_get_last_known_time(),
            staker_state_time = staking_get_last_known_time(),
            previous_staker_state_time = existing.staker_state_time,
            old_provider = staking_get_current_delegate(removed_delegation),
            new_provider = get_null_account(),
            balance = existing.balance,
            previous_balance = existing.balance,
            latest = true
        );
    }

    val name = provider_registration @ { .provider == provider } ( .name );
    create provider_registration(_staking_last_known_time(), provider, name, false);
}

operation staking_delegate(staker: ft4.accounts.account, provider: ft4.accounts.account) {
    require(staking_is_active_provider(provider), "Must be active provider.");
    _staking_delegate(staker, provider);
}

operation staking_undelegate(delegator: ft4.accounts.account) {
    _staking_delegate(delegator, get_null_account());
}

function _staking_delegate(delegator: ft4.accounts.account, provider: ft4.accounts.account) {
    require(staking_get_balance(delegator) > 0, "Must have a stake to staker.");

    // Clear any withdrawal request
    update staker_withdrawal_request @? {not .used and .staker == delegator} (used = true);

    // Get the values before messing with the flag
    val existing = staker_state @ {.latest and .staker == delegator};
    val current_provider = staking_get_current_delegate(delegator);

    // Clear flag from previous
    update staker_state @? {.staker == delegator and .latest} (latest = false);

    // New delegation record
    create staker_state(
        staker_state_time = staking_get_last_known_time(),
        staker = existing.staker,
        cutover_time = _staking_cannot_change_before(),
        previous_staker_state_time = existing.staker_state_time,
        new_provider = provider,
        old_provider = current_provider,
        balance = existing.balance,
        previous_balance = existing.balance,
        latest = true
    );
}

operation staking_cancel_withdrawal(staker: ft4.accounts.account) {
    update staker_withdrawal_request @? {not .used and .staker == staker} (used = true);
}

operation staking_claim_rewards() {
    // Get a list of stakers
    for(staker in staker_state @* {} (@group .staker) ) {
        val payout_amount = staking_rewards_claimable_for(staker);

        _pay_to(staker, payout_amount);

        // Record payout
        update staker_payout @* {.staker == staker} (latest = false);
        create staker_payout(staker_payout_time = _staking_last_known_time(), staker, payout_amount, latest = true);
    }
}

query staking_get_current_rates(): struct<reward_rate> {
    val current_rate = reward_rate @ {
        .reward_rate_time <= _staking_last_known_time()
    } (
        reward_rate.to_struct(),
        @omit @sort_desc .reward_rate_time
    ) limit 1;
    return current_rate;
}

query staking_get_current_delegate(staker: ft4.accounts.account): ft4.accounts.account {
    val current_state = staker_state @? { .staker == staker and .latest };
    return
        if  (null == current_state) get_null_account()
    else if ( staking_get_last_known_time() >= current_state.cutover_time ) current_state.new_provider
    else    current_state.old_provider;
}

function _pay_to(staker: ft4.accounts.account, amount: integer) {
    // We pay from the same pool account as provider rewards
    ft4.assets.Unsafe.transfer(staker, get_pool_account(), get_asset(), amount);
}

function _staking_cannot_change_before(): timestamp = _staking_last_known_time() + staking_config.rate_change_delay_ms;

query staking_get_cannot_change_before(): timestamp = _staking_cannot_change_before();

// As per https://learn.chromia.com/courses/latest-known-time
function _staking_last_known_time() 
    = if (op_context.exists) op_context.last_block_time else block @ {} (@max .timestamp) ?: 0;

query staking_get_last_known_time() = _staking_last_known_time();

// As suggested by https://chromadev.zulipchat.com/#narrow/stream/316358-Protocols/topic/Little.20questions/near/421469052
function get_null_account() {
    val NULL_ACCOUNT_ID = "NULL".hash();
    return ft4.accounts.account @? { NULL_ACCOUNT_ID } ?: create ft4.accounts.account(NULL_ACCOUNT_ID);
}