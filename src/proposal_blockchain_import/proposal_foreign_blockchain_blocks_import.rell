entity pending_foreign_blockchain_blocks_import {
    key proposal;
    blockchain: blockchain;
    up_to_height: integer;
}

@extend(apply_voting_result_handlers) function() = [proposal_type.foreign_blockchain_blocks_import.name: apply_foreign_blockchain_blocks_import(*)];
@extend(delete_proposal_handlers) function(): map<text, (proposal) -> unit> = [proposal_type.foreign_blockchain_blocks_import.name: delete_pending_foreign_blockchain_blocks_import(*)];

operation propose_foreign_blockchain_blocks_import(
    my_pubkey: pubkey, blockchain_rid: byte_array, up_to_height: integer, description: text = "Propose import foreign blockchain blocks"
) {
    val me = require_is_provider_with_rate_limit(my_pubkey);
    val blockchain = require_blockchain_entity(blockchain_rid);
    require(blockchain.state == blockchain_state.IMPORTING, "Blockchain must be in %s state to start importing blocks".format(blockchain_state.IMPORTING));

    val container = container_blockchain @ { blockchain }.container;
    require_container_deployer(container, me);

    require(exists(importing_foreign_blockchain @* { blockchain_rid }), "Can't find foreign blockchain being imported: %s".format(blockchain_rid));
    require(empty(pending_foreign_blockchain_blocks_import @* { blockchain, .proposal.state == proposal_state.PENDING } limit 1), "Already proposed");
    require_up_to_height_is_less_or_equals_last_config_height(blockchain, up_to_height);

    val prop = create proposal(op_context.last_block_time, proposal_type.foreign_blockchain_blocks_import, me, container.deployer, description);
    create pending_foreign_blockchain_blocks_import(prop, blockchain, up_to_height);
    internal_vote(me, prop, true);
}

function apply_foreign_blockchain_blocks_import(proposal) {
    val pfbbi = pending_foreign_blockchain_blocks_import @? { proposal };
    if (pfbbi == null) return;

    val container = container_blockchain @ { pfbbi.blockchain }.container;
    val nodes = cluster_node @* { container.cluster } (@sort .node.pubkey);
    require(nodes.size() > 0, "Cluster must have at least one node");
    require(exists(importing_foreign_blockchain @* { .blockchain_rid == pfbbi.blockchain.rid }),
        "Can't find foreign blockchain being imported: %s".format(pfbbi.blockchain.rid));
    require_up_to_height_is_less_or_equals_last_config_height(pfbbi.blockchain, pfbbi.up_to_height);
    val fbi = require(importing_foreign_blockchain @? { .blockchain_rid == pfbbi.blockchain.rid },
        "Can't find foreign blockchain being imported: %s".format(pfbbi.blockchain.rid)
    );
    require(pfbbi.up_to_height != -1,
        "A up_to_height (%s) already proposed for foreign blockchain import: %s".format(pfbbi.up_to_height, pfbbi.blockchain.rid)
    );

    update blockchain @ { pfbbi.blockchain.rid } (.state = blockchain_state.RUNNING);
    create blockchain_configuration_signers(pfbbi.blockchain, pfbbi.up_to_height + 1, nodes.to_gtv().to_bytes());
    fbi.up_to_height = pfbbi.up_to_height;
    create blockchain_configuration_options(
        pfbbi.blockchain, pfbbi.up_to_height + 1, suppress_special_transaction_validation = false
    );

    log("Foreign blockchain blocks import started: %s / %s".format(pfbbi.blockchain.name, pfbbi.blockchain.rid));
}

function delete_pending_foreign_blockchain_blocks_import(proposal) {
    delete pending_foreign_blockchain_blocks_import @? { proposal };
}

query get_foreign_blockchain_blocks_import_proposal(rowid?): (blockchain_rid: byte_array, up_to_height: integer)? {
    val proposal = get_latest_proposal(rowid, proposal_type.foreign_blockchain_blocks_import);
    if (proposal == null) return null;
    return pending_foreign_blockchain_blocks_import @ { proposal } (
        blockchain_rid = .blockchain.rid,
        up_to_height = .up_to_height
    );
}

function require_up_to_height_is_less_or_equals_last_config_height(blockchain, up_to_height: integer) {
    val last_config_height = require(blockchain_configuration @? { blockchain } (@sort_desc .height) limit 1,
        "No configurations found for importing blockchain: " + blockchain.rid
    );
    require(last_config_height <= up_to_height,
        "Proposed up_to_height (%s) must be greater or equal to the height (%s) of the latest imported configuration: %s"
        .format(up_to_height, last_config_height, blockchain.rid)
    );
}
