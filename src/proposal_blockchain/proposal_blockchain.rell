// Proposed bc:s are put here while waiting for enough positive votes.
entity pending_blockchain {
    key proposal;
    name;
    data: byte_array;
    container;
}

entity added_blockchain {
    key proposal;
    blockchain;
}

@extend(is_container_available_for_removal) function(container) = 
        if (exists(pending_blockchain @* { container }))
            "Container %s has pending proposals and can't be deleted. Resolve proposals first".format(container.name)
        else null;

/*
 * Proposes a new blockchain to a container.
 * NB: only the deployer voter set of the container can do this.
 */
operation propose_blockchain(my_pubkey: pubkey, config_data: byte_array, bc_name: text, container_name: text, description: text = "") {
    val me = require_is_provider_with_rate_limit(my_pubkey);
    val container = require_container(container_name);
    require_container_deployer(container, me);
    require(not container.system, "Proposing blockchain to a system container is not allowed");

    validate_blockchain_configuration(config_data, signers = true, header_hash = true);

    // Require container resource limits are honored
    require_container_is_not_full(container);

    require(empty(pending_blockchain @* { .data == config_data, .proposal.state == proposal_state.PENDING } limit 1), "Already proposed");
    val prop = create proposal(op_context.last_block_time, proposal_type.bc, me, container.deployer, description);
    create pending_blockchain(prop, bc_name, data = config_data, container);
    internal_vote(me, prop, true);
}

@extend(delete_proposal_handlers) function(): map<text, (proposal) -> unit> = [proposal_type.bc.name: delete_pending_blockchain(*)];

function delete_pending_blockchain(proposal) {
    delete pending_blockchain @? { proposal };
}

@extend(apply_voting_result_handlers) function() = [proposal_type.bc.name: apply_blockchain_proposal(*)];

// Initial signers of new bc are the ones in cluster_node table.
function apply_blockchain_proposal(proposal) {
    val bc = pending_blockchain @? {proposal};
    if (bc == null) return;
    val nodes = cluster_node @* { bc.container.cluster } (@sort .node.pubkey);
    // do not write new configuration when size is 0 since it's impossible to recover from that
    require(nodes.size() > 0, "Cluster must have at least one node");
    require_container_is_not_full(bc.container);

    val blockchain = add_blockchain(bc.data, nodes, bc.name, bc.container);
    create added_blockchain(proposal, blockchain);
    log("Added blockchain", blockchain.rid);
}

query get_blockchain_proposal(rowid): (data:byte_array, container:text)? {
    return pending_blockchain @? { require_proposal(rowid) } ( data = .data, container = .container.name );
}

/**
 * tx_rid: transaction rid of blockchain proposal
 * returns blockchain RID, or null if proposal is not applied
 */
query find_blockchain_rid(tx_rid: byte_array): byte_array? { 
    val proposal = require(proposal @? { proposal_type.bc, .transaction.tx_rid == tx_rid }, "No blockchain proposal found in given transaction");
    return added_blockchain @? { proposal }.blockchain.rid;
}
