entity pending_blockchain_move_finish {
    key proposal;
    blockchain;
    destination: container;
    final_height: integer;
}

operation propose_blockchain_move_finish(my_pubkey: pubkey, blockchain_rid: byte_array, final_height: integer, description: text = "") {
    val me = require_is_provider_with_rate_limit(my_pubkey);
    val blockchain = require_active_blockchain(blockchain_rid);
    val bc_move_state = require(moving_blockchain @? { blockchain }, "Blockchain %s is not being moved".format(blockchain.rid));

    val prop = create_proposal(proposal_type.blockchain_move_finish, me, bc_move_state.destination.deployer, description); // TODO: POS-961: src + dst?
    create pending_blockchain_move_finish(prop, blockchain, bc_move_state.destination, final_height);
    internal_vote(me, prop, true);
}

query get_blockchain_move_finish_proposal(rowid?):
        (blockchain_rid: byte_array, blockchain_name: text, cluster: text, container: text, final_height: integer)? {
    val proposal = get_latest_proposal(rowid, proposal_type.blockchain_import);
    if (proposal == null) return null;
    return pending_blockchain_move_finish @ { proposal } (
        blockchain_rid = .blockchain.rid,
        blockchain_name = .blockchain.name,
        cluster = .destination.cluster.name,
        container = .destination.name,
        final_height = .final_height
    );
}

@extend(apply_voting_result_handlers) function() = [proposal_type.blockchain_move_finish.name: apply_blockchain_move_finish(*)];
@extend(delete_proposal_handlers) function(): map<text, (proposal) -> unit> = [proposal_type.blockchain_move_finish.name: delete_pending_blockchain_move_finish(*)];

function apply_blockchain_move_finish(proposal) {
    val pbmf = pending_blockchain_move_finish @ { proposal } ( $.to_struct() );
    val mv_bc = require(moving_blockchain @? { pbmf.blockchain }, "Blockchain %s is not being moved".format(pbmf.blockchain.rid));

    // create a base_config and signers config at final_height (base_config needed if signers don't change)
    // FYI: PCU-based update (i.e. update_configuration_signers_regular()) can't be used here.
    // base config
    val base_config = require(get_blockchain_configuration(pbmf.blockchain.rid, pbmf.final_height)?.base_config,
        "Can't find config for %s for block %s".format(pbmf.blockchain.rid, pbmf.final_height));
    compress_and_store_configuration(pbmf.blockchain, pbmf.final_height + 1, make_config_unique(base_config));
    // signers config
    val encoded_dst_nodes = (cluster_node @* { pbmf.destination.cluster } (@sort .node.pubkey)).to_gtv().to_bytes();
    create blockchain_configuration_signers(pbmf.blockchain, pbmf.final_height + 1, encoded_dst_nodes);

    // update moving_blockchain state
    mv_bc.final_height = pbmf.final_height;
    mv_bc.remove_on_nodes = (cluster_node @* { mv_bc.source.cluster } (@sort .node.pubkey )).to_gtv().to_bytes();
}

function delete_pending_blockchain_move_finish(proposal) {
    delete pending_blockchain_move_finish @? { proposal };
}
