query get_proposals_since(since: rowid) {
    return proposal @* { .rowid >= since, .state == proposal_state.PENDING } (.rowid, .proposal_type);
}

query get_all_proposals_since(since: rowid) {
    return proposal @* { .rowid >= since } (.rowid, .proposal_type, .state );
}

query get_relevant_proposals(my_pubkey: pubkey, since: rowid) {
    return (proposal, voter_set_member) @* {
        voter_set_member.voter_set == proposal.voter_set,
        voter_set_member.provider.pubkey == my_pubkey,
        proposal.rowid >= since,
        proposal.state == proposal_state.PENDING
    } (proposal.rowid, proposal.proposal_type);
}

query get_proposal(id: rowid?): (id: rowid, timestamp: integer, type: proposal_type, proposed_by: pubkey, description: text)? {
    val result = if (id == null) proposal @? {} (@sort_desc @omit .rowid, $) limit 1 else proposal @? { id };
    if (result == null) return null;
    return (
        id = result.rowid,
        timestamp = result.timestamp,
        type = result.proposal_type,
        proposed_by = result.proposed_by.pubkey,
        description = result.description
    );
}

function require_proposal(rowid) = require(proposal @? { rowid }, "Proposal " + rowid + " not found");

function get_latest_proposal(rowid?, proposal_type) = if (rowid == null) proposal @ { proposal_type, .state == proposal_state.PENDING } ( @max proposal ) else proposal @ { rowid };

struct proposal_voting_results {
    positive_votes: integer;
    negative_votes: integer;
    max_votes: integer;
    threshold: integer;
    voting_result;
}

query get_proposal_voting_results(rowid): proposal_voting_results {
    val proposal = require_proposal(rowid);
    val positive_votes = positive_votes(proposal);
    val negative_votes = negative_votes(proposal);
    val max_votes = max_votes(proposal.voter_set);
    val threshold = proposal.voter_set.threshold;
    val status = _compute_voting_result(positive_votes, negative_votes, max_votes, threshold);
    return proposal_voting_results(positive_votes, negative_votes, max_votes, threshold, status);
}
